plugins {
    id "groovy"
    id 'java'
    id "idea"
}

group = 'dev.crifurch'
version = '0.3.2'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://maven.enginehub.org/repo/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven{
        url 'https://maven.elmakers.com/repository/'
    }
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}


dependencies {
    extraLibs 'com.github.Querz:NBT:6.1'
    compileOnly 'org.bukkit:bukkit:1.15.2-R0.1-SNAPSHOT'
    compileOnly group: 'com.github.MilkBowl', name: 'VaultAPI', version: '1.7.1'
    compileOnly 'org.jetbrains:annotations:20.1.0'
    configurations.compileOnly.extendsFrom(configurations.extraLibs)
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
